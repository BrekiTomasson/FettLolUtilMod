plugins {
    id 'fabric-loom' version '0.6-SNAPSHOT'
    id 'maven-publish'
}

repositories {
    maven {
        name = "Siphalor's Maven"
        url  = "https://maven.siphalor.de"
    }
    maven {
        name = "TerraformersMC"
        url  = "https://maven.terraformersmc.com/releases"
    }
    maven {
        name = "Shedaniel"
        url  = "https://maven.shedaniel.me/"
    }
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

mod_id = project.mod_id
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

loom {
    accessWidener "src/main/resources/fettlol.accesswidener"
}

dependencies {
    // Minecraft and its related mappings.
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    // Fabric API and Fabric Loader
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    // Dependencies etc.
    include modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") { exclude(group: "net.fabricmc.fabric-api") }

    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}") { exclude module: 'fabric-api' }
    modImplementation "de.siphalor:nbtcrafting-${project.nbtcrafting_version}"

    include implementation("com.moandjiezana.toml:toml4j:${project.toml4j_version}")
    include implementation("com.electronwill.night-config:core:${project.tomlnc_version}")
    include implementation("com.electronwill.night-config:toml:${project.tomlnc_version}")
}

processResources {
    inputs.property "version", project.version
    inputs.property "name", project.name

    filesMatching("fabric.mod.json") {
        expand(
                "version": project.version,
                "minecraft_version": project.minecraft_version,
                "maven_group": project.maven_group,
                "name": project.name,
                "loader_version": project.fabric_loader_version,
                "mod_id": project.mod_id,
                "init_class": project.init_class,
                "author": project.author,
                "fabric_api_version": project.fabric_api_version
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.mod_id}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
    }
}
